# BASELINE
set shell := ["bash", "-euo", "pipefail", "-c"]

alias i := install
alias k := knip
alias tc := typecheck
alias l := lint
alias t := test
alias c := check
alias u := upgrade
alias ui := upgrade-interactive
alias p := push
alias pr := push-ready

# List available recipes.
default:
    @just --list

# Install both workspaces: pnpm + uv.
install:
    pnpm install
    uv sync --all-packages --all-groups

# Report unused files, deps, and exports: knip (JS workspace, default + prod pass) + vulture (Python).
knip:
    pnpm run knip
    pnpm run knip --config knip.prod.json
    uv run vulture

# Type-check both workspaces: tsc for .ts, pyrefly for .py.
typecheck:
    pnpm run typecheck
    uv run pyrefly check

# Lint and format both workspaces with autofix.
lint:
    pnpm run lint:fix
    pnpm run format
    uv run rumdl check --fix
    uv run rumdl fmt
    uv run ruff check --fix
    uv run ruff format

# Run tests for both workspaces, JS and Python in parallel. Optional arg filters by test name, skipping coverage; never fails when nothing matches.
test name='':
    pnpm run {{ if name == '' { '--silent cz test' } else { 'cz test ' + quote(name) } }}

# Verify org invariants with cerberus, over the coverage report `test` regenerates.
cerberus:
    uv run cerberus --fix

# Full gate across both workspaces: install, knip, typecheck, lint, test, cerberus — autofix throughout.
check: install knip typecheck lint test cerberus

# Upgrade toolchains and workspace dependencies.
upgrade *args='':
    pnpm run --silent cz upgrade {{ args }}

# Interactively select toolchain and JavaScript upgrades.
upgrade-interactive:
    pnpm run --silent cz upgrade --interactive

# Push the current branch and open a draft PR (-r/--ready marks it ready and enables auto-merge).
push *flags:
    pnpm run cz push-branch {{ flags }}

# Push the current branch and open a PR marked ready, enabling auto-merge.
push-ready: (push "--ready")

# Remove gitignored build artifacts and caches from all workspaces.
clean *flags:
    pnpm run cz clean {{ flags }}

# CUSTOM

# Upsert every org ruleset in rulesets/ to GitHub (source of truth). Needs gh authenticated with org-admin scope.
apply-org-ruleset:
    pnpm exec node apps/apply-org-rulesets/src/index.ts
